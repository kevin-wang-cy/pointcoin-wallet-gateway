buildscript {
    ext {
        springBootVersion = '1.4.3.RELEASE'
    }

    repositories {
        maven {
            url "${nexusPublicUrl}"
        }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

allprojects {
    group = 'com.upbchain.pointcoin'
    version = project.properties.containsKey("releaseVersion") ? "${releaseVersion}" : "2.0-SNAPSHOT"

    apply plugin: 'idea'

    repositories {
        maven {
            url "${nexusPublicUrl}"
        }
    }
    
    task wrapper(type: Wrapper) {
        gradleVersion = '3.4.1'
    }
}

description = 'Pointcoin Wallet Gateway'

subprojects {
    ext {
        archivesBaseName = "${rootProject.name}-${project.name}"

        nexusRepoUrl = project.properties.containsKey("releaseVersion") ? "${nexusReleaseUrl}" : "${nexusSnapshotUrl}"

        jasyptBootVersion = '1.10'

        mysqlConnectorVersion = "5.1.40"
        druidVersion = "1.0.27"

        pointcoinwalletcommonVersion = "2.0-SNAPSHOT"
    }

    apply plugin: 'java'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    jar {
        baseName "${project.archivesBaseName}"
    }

    apply plugin: 'eclipse'
    eclipse {
        project {
            name = "${rootProject.name}-${project.name}"
        }
    }

    apply plugin: 'maven-publish'

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId "${project.group}"
                artifactId "${rootProject.name}-${project.name}"
                from components.java
            }
        }
        repositories {
                maven {
                    credentials {
                        username "${nexusUsername}"
                        password "${nexusPassword}"
                    }
                    url "${nexusRepoUrl}"
                }
        }
    }

    publish {
        dependsOn assemble
    }
}

